""" SCVUS v2 16S alternate metabarcode pipeline, part 2

This is the continuation of the first part of the alternate meteabarcode pipeline:
6_Create ESV table
7_Taxonomic assignment

"""

#######################################################################
# Read in vars from config.yaml
# Generate new vars, dirs, and filenames
# All code not in rules (this section) is run first before any rules (next section onwards)

dir=config["dir"]

# Primer trimmed reads
gzip_out=dir+"/cat.fasta2.gz"

# Denoised ESVs
usearch_out=dir+"/cat.denoised"

# 6_Create ESV table
vsearch_out2=dir+"/ESV.table"
vsearch_log2=dir+"/table.log"

# 7_Taxonomic assignment
rdp_out=dir+"/rdp.out.tmp"
rdp_csv=dir+"/rdp.csv.tmp"
rdp_csv2=dir+"/rdp.csv"

#######################################################################
# This rule defines the final target file that is needed from the pipeline
# Hashed lines (begin with # symbol) are not executed
# Target files need to be separated with a comma
# Snakemake looks at all input and output files to determine which rules need to be run to create the target file

rule all:
	input:
		# Rule testing [In order of execution]:
		# 6_Create ESV table
#		vsearch_out2
		# 7_Taxonomic assignment
#		rdp_out
		# 7_Taxonomic assignment (add read numbers)
#		rdp_csv
		# 7_Taxonomic assignment (edit ESV id's to include amplicon name) [Final output file]
		rdp_csv2
	 
#######################################################################
# Create ESV table

rule create_ESV_table:
	version: "2.13.16"
	input:
		usearch_global=gzip_out,
		db=usearch_out
	output:
		vsearch_out2
	threads: config["VSEARCH"]["t"]
	log: vsearch_log2
	shell:
		"vsearch --threads {threads} --usearch_global {input.usearch_global} --db {input.db} --id 1.0 --otutabout {output} --log {log}"

#######################################################################
# Taxonomic assignment

# Use the RDP classifier for taxonomic assignment
# Make sure this is installed locally (do not use the old 2.2 version available from conda), download the v2.12 from sourceforge, indicate the path to the classifeir in the config file
# Be sure to indicate the location of the trained reference database properties file in the config file

rule taxonomic_assignment:
	version: "2.12"
	input:
		usearch_out
	output:
		temp(rdp_out)
	shell:
		"java -Xmx8g -jar {config[RDP][jar]} classify -c {config[RDP][c]} -f {config[RDP][f]} -o {output} {input}"
	
#######################################################################
# Map read number

# Edit path in top line of Perl script to match path to perl in conda environment

rule map_read_number:
	input:
		table=vsearch_out2,
		rdp=rdp_out
	output:
		temp(rdp_csv)
	shell:
		"perl perl_scripts/add_abundance_to_rdp_out4b.plx {input.table} {input.rdp} > {output}"

#######################################################################
# Edit csv to add amplicon name to Zotu
# This is needed when data from different amplicons are combined, so that the Zotu's remain unique

rule edit_csv:
	input:
		rdp_csv
	output:
		rdp_csv2
	shell:
		"sed -e {config[SED]} {input} > {output}"


# End of alternate metabarcode pipeline, part 2
